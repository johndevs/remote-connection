allprojects {
	group='fi.jasoft'
	version = project.hasProperty('BUILD_VERSION') ? getProperty('BUILD_VERSION') : 'development';
}

subprojects {
	apply from: 'http://plugins.jasoft.fi/vaadin.plugin'
	apply plugin: 'eclipse-wtp'
	sourceCompatibility = 1.6
	targetCompatibility = 1.6
    basename = 'remote-connection'
    vaadin {
        version '7.6.1'
        gwt {
            userAgent 'gecko1_8,safari'
            strict true
        }
    }
}

project(':remote-connection'){
	vaadin {
		widgetset 'fi.jasoft.remoteconnection.RemoteConnectionWidgetset'
        addon {
			author 'John Ahlroos'
			license 'Apache 2.0'
			title 'RemoteConnection'
		}
	}

	jar{
		// Include source in jar
		from sourceSets.main.allJava
	}
    artifacts {
        archives jar
    }
    uploadArchives {
        dependsOn createAddonPom
        repositories.mavenDeployer {
            def snapshotRepositoryUrl = System.getProperty('PLUGIN_MAVEN_SNAPSHOT_REPOSITORY')
            def snapshotRepositoryUser = System.getProperty('PLUGIN_MAVEN_SNAPSHOT_REPOSITORY_USER')
            def snapshotRepositoryPassword = System.getProperty('PLUGIN_MAVEN_SNAPSHOT_REPOSITORY_PASSWORD')
            def snapshotRepositoryPrivateKey = System.getProperty('PLUGIN_MAVEN_SNAPSHOT_REPOSITORY_KEY')

            configuration = configurations.deploy

            snapshotRepository( url: snapshotRepositoryUrl ) {
                authentication(
                     userName: snapshotRepositoryUser,
                     password: snapshotRepositoryPassword,
                     privateKey: snapshotRepositoryPrivateKey
                )
            }
            addFilter('jar') {artifact, file ->
                artifact.ext == 'jar'
            }
        }
    }		
}

project(':server-example'){
	dependencies {
		compile project(':remote-connection')
	}
	vaadin {
		widgetset 'fi.jasoft.remoteconnection.ServerExampleWidgetset'
	}
    war {
        archiveName = 'RemoteConnection-Server.war'
    }
}

project(':client-example'){
    dependencies {
	    compile project(path: ':remote-connection')
    }              
    vaadin {
        widgetset 'fi.jasoft.remoteconnection.ClientExampleWidgetset'
    }
    war {
        archiveName = 'RemoteConnection-Client.war'
    }       
}

project(':chatroom'){
    dependencies {
        compile project(':remote-connection')
    }
    vaadin {
        widgetset 'fi.jasoft.chatroom.ChatRoomWidgetset'
    }
    war {
        archiveName = 'Chatroom.war'
    }
}


